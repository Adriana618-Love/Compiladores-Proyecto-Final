S ::= code $

code ::= statement code_p
code_p ::= statement code_p
code_p ::= lambda
code ::= lambda

statement ::= bucle
statement ::= conditional
statement ::= declaration endline
statement ::= assigment endline
statement ::= expression endline
statement ::= other_statement endline

other_statement ::= Print ( expression )

bucle ::= while ( expression ) { code }

conditional ::= if ( expression ) { code } conditional_p
conditional_p ::= else { code }
conditional_p ::= lambda

declaration ::= d_number variable declaration_p
declaration ::= d_string variable declaration_p
declaration ::= d_video variable declaration_p
declaration ::= d_video_text variable declaration_p
declaration_p ::= ( expression )
declaration_p ::= lambda

assigment ::= set variable ( expression )

expression ::= ( expression ) expression_p
expression ::= variable expression_variable expression_p
expression_variable ::= . expression_variable_p
expression_variable_p ::= subclip ( expression , expression_2 )
expression_2 ::= expression
expression_variable_p ::= set_start ( expression )
expression_variable_p ::= volumex ( expression )
expression_variable_p ::= set_video_duration ( expression )
expression_variable_p ::= show_frame ( expression )
expression_variable_p ::= write_video ( expression )
expression_variable_p ::= video_preview ( expression )
expression_variable_p ::= video_concatenate ( expression )
expression_variable ::= lambda
expression ::= number expression_p
expression ::= string expression_p
expression_p ::= operator expression
expression_p ::= lambda

operator ::= *
operator ::= /
operator ::= +
operator ::= -
operator ::= <
operator ::= >
operator ::= <=
operator ::= >=
operator ::= ==
operator ::= &&
operator ::= ||

endline ::= ;
